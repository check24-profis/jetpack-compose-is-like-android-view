if (project.file('../secrets.gradle').exists()) {
    apply from: '../secrets.gradle'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "de.check24.compose.demo"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    // region signing configs

    signingConfigs {

        debug {
            if (project.hasProperty('STORE_FILE')) storeFile file(STORE_FILE)
            if (project.hasProperty('STORE_PASSWORD')) storePassword STORE_PASSWORD
            if (project.hasProperty('KEY_ALIAS')) keyAlias KEY_ALIAS
            if (project.hasProperty('KEY_PASSWORD')) keyPassword KEY_PASSWORD
        }

        // test-release signing config so that release can be build
        release {
            if (project.hasProperty('STORE_FILE')) storeFile file(STORE_FILE)
            if (project.hasProperty('STORE_PASSWORD')) storePassword STORE_PASSWORD
            if (project.hasProperty('KEY_ALIAS')) keyAlias KEY_ALIAS
            if (project.hasProperty('KEY_PASSWORD')) keyPassword KEY_PASSWORD
        }
    }

    // endregion

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            crunchPngs false
            shrinkResources false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    // region java compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
        }
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                // disable linting for opt-in annotations
                "-Xopt-in=kotlin.RequiresOptIn",
                // disable compose version check
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
        ]
        useIR = true
    }

    // endregion

    // region app bundles

    bundle {
        density {
            enableSplit true
        }
        abi {
            enableSplit true
        }
        language {
            enableSplit false
        }
    }

    // endregion

    buildFeatures {
        compose true
        viewBinding = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion KOTLIN_COMPILER_EXTENSION_VERSION
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // region lint

    lintOptions {
        abortOnError false
    }

    // endregion
}

apply from: "buildSrc/compose_dependencies.gradle"

dependencies {

    implementation "androidx.navigation:navigation-compose:2.4.1"
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'com.google.android.material:material:1.6.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation "com.google.maps.android:maps-compose:1.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-runtime-ktx:2.4.1'
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"
    implementation "androidx.fragment:fragment-ktx:1.4.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}